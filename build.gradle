/**
 * CurrencyBG Build Script
 */

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

apply plugin: 'com.android.application'

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.javaCompiler.dependsOn(copyAssets)
    }
}

android {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        compile 'com.android.support:design:23.1.1',
                'com.android.support:preference-v14:23.1.1',
                'com.melnykov:floatingactionbutton:1.3.0',
                'com.wdullaer:materialdatetimepicker:3.1.1',
                'com.afollestad.material-dialogs:core:0.8.6.2',
                'com.google.guava:guava:22.0-android',
                'org.jsoup:jsoup:1.10.3',
                'com.squareup.okhttp:okhttp:2.7.2',
                'com.google.code.gson:gson:2.8.1',
                'joda-time:joda-time:2.9.9'

        testCompile 'junit:junit:4.+'
    }

    compileSdkVersion 26
    buildToolsVersion '26.0.3'

    defaultConfig {
        applicationId 'net.vexelon.currencybg.app'
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 4002
        versionName "4.0.0"
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            assets.srcDirs = ['src/main/assets']
            java.srcDirs = ['src/main/java', 'src/main/test']
            resources.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main/java']
            renderscript.srcDirs = ['src/main/java']
        }
        //androidTest.setRoot('tests')
    }
    signingConfigs {
        release {
            storeFile file(KS)
            storePassword KSPWD
            keyAlias KALIAS
            keyPassword KPWD
        }
    }
    buildTypes {
        debug {
            dexOptions.preDexLibraries = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }
}

task copyAssets {
    doLast {
        println "Copying assets ..."
        copy {
            from 'api.properties'
            into 'src/main/assets'
        }
    }
}

